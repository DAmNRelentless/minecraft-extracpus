buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }

}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "org.sonarqube" version "3.3"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'

sonarqube {
    properties {
        property "sonar.projectKey", "RLNT_minecraft_extracpus"
        property "sonar.organization", "rlnt"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

version = "${minecraft_version}-${mod_version}"
group = "${mod_base_package}.${mod_id}"
archivesBaseName = mod_id

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {
            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    maven {
        name 'Cursemaven'
        url 'https://www.cursemaven.com/'
    }
}

dependencies {
    // minecraft
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    // applied energistics
    compile fg.deobf("curse.maven:appeng-223794:2747063")
    // extra cells
    // runtimeOnly fg.deobf("curse.maven:extracellsfork-485225:3329212")
    // forgelin
    // runtimeOnly fg.deobf("curse.maven:forgelin-248453:2727070")
    // ae additions - requires forgelin
    // runtimeOnly fg.deobf("curse.maven:aeadditions-493962:3430042")
    // jei
    // runtimeOnly fg.deobf("curse.maven:jei-238222:3043174")
}

// This block of code expands all the gradle properties in the specified resource targets.
// It copies them into the targets and expands all the defined properties.
def resourceTargets = ["mcmod.info", "pack.mcmeta"]
def intoTargets = ["$rootDir/out/production/resources/", "$rootDir/out/production/${project.name}.main/", "$rootDir/bin/main/"]
def replaceProperties = [
    mod_id           : mod_id,
    mod_name         : mod_name,
    mod_version      : mod_version,
    mod_authors      : mod_authors,
    mod_description  : mod_description,
    minecraft_version: minecraft_version,
    github_user      : github_user,
    github_repo      : github_repo
]
processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }

    intoTargets.each { target ->
        if (file(target).exists()) {
            copy {
                from(sourceSets.main.resources) {
                    include resourceTargets
                    expand replaceProperties
                }
                into target
            }
        }
    }
}

// fill manifest with information
jar {
    manifest {
        attributes([
            "Specification-Title"     : mod_id,
            "Specification-Vendor"    : mod_authors,
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : mod_version,
            "Implementation-Vendor"   : mod_authors,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

curseforge {
    if (System.getenv('CF_API_KEY') != null) {
        // GitHub actions
        apiKey = System.getenv('CF_API_KEY')
    } else {
        // error prevention
        apiKey = "none"
    }
    project {
        id = '408089'
        changelogType = 'markdown'
        changelog = 'The whole changelog can be found [here](https://github.com/RLNT/minecraft_extracpus/blob/main/CHANGELOG.md).'
        releaseType = 'release'
        addGameVersion minecraft_version

        mainArtifact(jar) {
            displayName = "ExtraCPUs-${minecraft_version}-${mod_version}"
        }
    }
}

jar.finalizedBy('reobfJar')

idea {
    module {
        outputDir = file('build/classes/main')
    }
}
